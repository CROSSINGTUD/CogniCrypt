abstract Enum

//enum Security
abstract Security : Enum -> integer
Broken: Security = 1
Weak: Security = 2
Medium: Security = 3
Strong: Security = 4

//enum Performance
abstract Performance : Enum -> integer
VerySlow: Performance = 1
Slow: Performance = 2
Fast: Performance = 3
VeryFast: Performance = 4

//enum Padding
abstract Padding: Enum 
NoPadding : Padding
PKCS5Padding : Padding

//enum Mode
abstract Mode : Enum
ECB : Mode
CBC : Mode
OFB : Mode
GCM : Mode

abstract Algorithm
	name -> string
	description -> string
	security -> Security
	performance -> Performance
	classPerformance -> Performance 


abstract Cipher : Algorithm

abstract SymmetricCipher : Cipher
	keySize -> integer
	[classPerformance = Fast]

abstract SymmetricBlockCipher : SymmetricCipher
	mode -> Mode
	padding -> Padding
	[mode != ECB]
	[padding != NoPadding]

abstract Task
    description -> string

xor Ciphers
	AES: SymmetricBlockCipher
		[description = "Advanced Encryption Standard (AES) cipher"]
		[name = "AES"]
		[keySize = 128 || 
			keySize = 192 || 
			keySize = 256]
		[keySize = 128 => performance = VeryFast 
							&& security = Medium]
		[keySize > 128 => performance = Fast 
							&& security = Strong]

	DES: SymmetricBlockCipher
	    [description = "DES encryption"]
	    [name = "DES"]
	    [performance = VeryFast ]
	    [security = Broken ]
	    [keySize = 56 ]

SymmetricEncryption : Task
	[description = "Encrypt data using a secret key"]
	cipher -> SymmetricBlockCipher